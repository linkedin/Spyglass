apply plugin: 'com.android.library'
apply plugin: 'idea'
apply plugin: 'maven'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize '2048m'
    }

    testOptions.unitTests.all {
        // configure the set of classes for JUnit tests
        include '**/*Test.class'
        exclude '**/espresso/**/*.class'

        // configure max heap size of the test JVM
        maxHeapSize = '2048m'

        // configure the test JVM arguments
        jvmArgs '-XX:MaxPermSize=512m', '-XX:-UseSplitVerifier'

        // Specify max number of processes (default is 1)
        maxParallelForks = 4

        // Specify max number of test classes to execute in a test process
        // before restarting the process (default is unlimited)
        forkEvery = 150

        // configure whether failing tests should fail the build
        ignoreFailures false

        // use afterTest to listen to the test execution results
        afterTest { descriptor, result ->
            println "Executing test for ${descriptor.name} with result: ${result.resultType}"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:support-annotations:25.0.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.robolectric:robolectric:3.1.3'
    testCompile 'org.robolectric:shadows-support-v4:3.1.3'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

apply from: 'gradle-mvn-push.gradle'
